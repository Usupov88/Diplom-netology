
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
	// {{ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 09.01.2024 ++ 
	НоменклатураРабСпец = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();	
	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Если ТекСтрокаУслуги.Номенклатура = НоменклатураРабСпец Тогда
			Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.ВКМ_Клиент = Контрагент;
			Движение.ВКМ_Договор = Договор;
			Движение.ВКМ_СуммаКОплате = ТекСтрокаУслуги.Сумма;
			Движение.ВКМ_КоличествоЧасов = ТекСтрокаУслуги.Количество;
		КонецЕсли;
	КонецЦикла;
	// }}ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 09.01.2024 --
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

 // {{ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 08.01.2024 ++
 Процедура ВыполнитьАвтозаполнение()Экспорт
	 
	 НомАбПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	 НомРабСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_СуммаКОплатеПриход) КАК СуммаКОплате,
	 |	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_КоличествоЧасовПриход) КАК КоличествоЧ
	 |ИЗ
	 |	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоПериода, &КонецПериода, , ВКМ_Договор = &Договор) КАК ВКМ_ВыполненныеКлиентуРаботыОбороты
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ДоговорыКонтрагентов.ВКМ_АбонентскаяПлата КАК АбонентскаяПлата
	 |ИЗ
	 |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	 |ГДЕ
	 |	ДоговорыКонтрагентов.Ссылка = &Ссылка";
	 
	 Запрос.УстановитьПараметр("Ссылка", Договор);
	 Запрос.УстановитьПараметр("Договор", Договор);
	 Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	 Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	 
	 Выборка = Запрос.ВыполнитьПакет();
	 
	 ВыборкаНомРабСпециалиста = Выборка[0].Выбрать();
	 ВыборкаНомАбПлата = Выборка[1].Выбрать();	
	 
	 Услуги.Очистить();
	 Пока ВыборкаНомАбПлата.Следующий() Цикл
		 
		 Если ЗначениеЗаполнено(НомАбПлата) Тогда	
			 Строка = Услуги.Добавить();		
			 Строка.Номенклатура = НомАбПлата;
			 Строка.Количество = 1;
			 Строка.Сумма = ВыборкаНомАбПлата.АбонентскаяПлата;
			 Строка.Цена = ВыборкаНомАбПлата.АбонентскаяПлата;
		 КонецЕсли;
		 
	 КонецЦикла;
	 
	 Пока ВыборкаНомРабСпециалиста.Следующий() Цикл
		 
		 Если ЗначениеЗаполнено(НомРабСпециалиста) Тогда
			 Строка = Услуги.Добавить();
			 Строка.Номенклатура = НомРабСпециалиста;			 
			 Строка.Количество = ВыборкаНомРабСпециалиста.КоличествоЧ;
			 Строка.Сумма = ВыборкаНомРабСпециалиста.СуммаКОплате;
			 Строка.Цена = ?(Строка.Количество = 0, 0, Строка.Сумма / Строка.Количество);
		 КонецЕсли;
	 
	 КонецЦикла;
	
КонецПроцедуры
// }}ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 08.01.2024 --

#КонецОбласти

#КонецЕсли
