
#Область ОбработчикиСобытийФормы

 // {{ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 01.02.2024 ++
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	МассивСотрудников = ПолучитьРазницуКоличестваДнейОтпускаСотрудников(); 
	УстановитьПодсветкуКолонки(МассивСотрудников); 
	
КонецПроцедуры
 // }}ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 01.02.2024 -- 
 
# КонецОбласти 

#Область СлужебныеПроцедурыФункции

 // {{ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 01.02.2024 ++
&НаКлиенте
Процедура ВКМ_ГрафОтпусков(Команда)  
	
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.ВКМ_АнализГрафика",, ЭтаФорма); 
	
КонецПроцедуры

&НаСервере
Функция ВыгружаемТЧОтпускаСотрудников()Экспорт 
	
	ОтпускаСотрудников = Новый Массив;
	Для каждого Строка Из Объект.ВКМ_ОтпускаСотрудников Цикл
		
		Сотрудники = Новый Структура;
		Сотрудники.Вставить("ВКМ_Сотрудник", Строка.ВКМ_Сотрудник);
		Сотрудники.Вставить("ВКМ_ДатаНачала", Строка.ВКМ_ДатаНачала);
		Сотрудники.Вставить("ВКМ_ДатаОкончания", Строка.ВКМ_ДатаОкончания);
		
		ОтпускаСотрудников.Добавить(Сотрудники);
	КонецЦикла;
	
	Адрес =  ПоместитьВоВременноеХранилище(ОтпускаСотрудников, Новый УникальныйИдентификатор);
	Возврат Адрес;
	
КонецФункции

&НаСервере
Функция ПолучитьРазницуКоличестваДнейОтпускаСотрудников() 	
	
	МассивСотрудников = Новый Массив;
	СотрудникиТЧ = Объект.ВКМ_ОтпускаСотрудников.Выгрузить(); 
	
	ОписаниеТипа = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3));
	СотрудникиТЧ.Колонки.Добавить("Количество", ОписаниеТипа);
	
	Для Каждого СтрТЗ Из СотрудникиТЧ Цикл
		
		СтрТЗ.Количество = (СтрТЗ.ВКМ_ДатаОкончания - СтрТЗ.ВКМ_ДатаНачала) / 86400;
		
	КонецЦикла;
	
	СотрудникиТЧ.Свернуть("ВКМ_Сотрудник", "Количество");
	
	Для Каждого СтрокаТЗ Из СотрудникиТЧ Цикл 
		
		Если СтрокаТЗ.Количество > 28 Тогда
			Сообщить(СтрШаблон("У сотрудника %1 количество дней отпуска превышает 28 дней . И составляет %2 дней",
			                                                         СтрокаТЗ.ВКМ_Сотрудник, СтрокаТЗ.Количество));
			МассивСотрудников.Добавить(СтрокаТЗ.ВКМ_Сотрудник);												  
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивСотрудников;
	
КонецФункции

&НаСервере
Процедура УстановитьПодсветкуКолонки(МассивСотрудников)
	
	УсловноеОформление.Элементы.Очистить();
	
	Для Каждого ЭлементМассива Из МассивСотрудников Цикл
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Использование = Истина;
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.Красный);
		
		ЭлементУсловия                = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементУсловия.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ВКМ_ОтпускаСотрудников.ВКМ_Сотрудник");
		ЭлементУсловия.ПравоеЗначение = ЭлементМассива;
		ЭлементУсловия.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;   
		ЭлементУсловия.Использование  = Истина;
		
		ОформляемоеПоле      = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ВКМ_ОтпускаСотрудниковВКМ_Сотрудник");
		
	КонецЦикла;
	
КонецПроцедуры  
// }}ВКМ_ВашКомпьютерныйМастер, доработка - Юсупов А.Г 01.02.2024 --

# КонецОбласти 
