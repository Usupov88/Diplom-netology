
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗаполнитьПериод();
	ЗаполнитьИзмерения();
	ЗаполнитьКаландарьСпециалистов();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиентов" Тогда
		ЗаполнитьКаландарьСпециалистов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВКМ_ВремяС", Начало);
	ЗначенияЗаполнения.Вставить("ВКМ_По", Конец);
	ЗначенияЗаполнения.Вставить("ВКМ_ЧасыКОплатеКлиенту", Конец - Начало);
	ЗначенияЗаполнения.Вставить("ВКМ_Специалист", ЗначенияИзмерений["ВКМ_Сотрудник"]);
	
	СтруктураПланировщика = Новый Структура("ЗначенияЗаполнения",ЗначенияЗаполнения);
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиентов.Форма.ФормаДокумента", 
	                         СтруктураПланировщика,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
 КонецПроцедуры
						 
&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	СтруктураПараметров = Новый Структура("Ключ", ЭлементПланировщика.Значение);
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиентов.Форма.ФормаДокумента", 
	                       СтруктураПараметров,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры						 

 #КонецОбласти
 
 #Область СлужебныеПроцедурыИФункции
   
 &НаСервере
Процедура ЗаполнитьПериод() 
	
	Планировщик.ЕдиницаПериодическогоВарианта = ТипЕдиницыШкалыВремени.Час;	
	Планировщик.ОтступСНачалаПереносаШкалыВремени = 9;
	Планировщик.ОтступСКонцаПереносаШкалыВремени = 6; 
	Планировщик.КратностьПериодическогоВарианта = 24;
	
	НачалоПериода = НачалоНедели(ТекущаяДата());
	КонецПериода = КонецНедели(ТекущаяДата())-2*24*60*60;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзмерения()

	Измерения = Планировщик.Измерения.Добавить("ВКМ_Сотрудник");
	Выборка = Справочники.ВКМ_Сотрудники.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗначениеИзмерения = Измерения.Элементы.Добавить(Выборка.Ссылка);
		ЗначениеИзмерения.Текст = Выборка.Наименование + Символы.ПС;
		ЗначениеИзмерения.ЦветРамки = WebЦвета.Зеленый;
		
	КонецЦикла;  

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКаландарьСпециалистов()
	
	ЭлементыПланировщика = Планировщик.Элементы;
	ЭлементыПланировщика.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиентов.Ссылка КАК Ссылка,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_Клиент КАК ВКМ_Клиент,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_Договор КАК ВКМ_Договор,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_Специалист КАК ВКМ_Специалист,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_ПроведениеРабот КАК ВКМ_ПроведениеРабот,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_ВремяС КАК ВКМ_ВремяС,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_По КАК ВКМ_По,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_ОписаниеПроблемы КАК ВКМ_ОписаниеПроблемы,
	|	ВКМ_ОбслуживаниеКлиентовВКМ_ВыполненыеРаботы.ВКМ_ОписаниеРабот КАК ВКМ_ОписаниеРабот,
	|	ВКМ_ОбслуживаниеКлиентовВКМ_ВыполненыеРаботы.ВКМ_ЧасыКОплатеКлиенту КАК ВКМ_ЧасыКОплатеКлиенту,
	|	ВКМ_ОбслуживаниеКлиентов.ВКМ_Цвет КАК ВКМ_Цвет
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиентов.ВКМ_ВыполненыеРаботы КАК ВКМ_ОбслуживаниеКлиентовВКМ_ВыполненыеРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВКМ_ОбслуживаниеКлиентов КАК ВКМ_ОбслуживаниеКлиентов
	|		ПО ВКМ_ОбслуживаниеКлиентовВКМ_ВыполненыеРаботы.Ссылка = ВКМ_ОбслуживаниеКлиентов.Ссылка
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиентов.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачалоПериода = Выборка.ВКМ_ПроведениеРабот + (Выборка.ВКМ_ВремяС - Дата(1, 1, 1));
		КонецПериода = Выборка.ВКМ_ПроведениеРабот + (Выборка.ВКМ_По - Дата(1, 1, 1));
		  		
		СоответствиеИзмерений = Новый Соответствие;
		СоответствиеИзмерений.Вставить("ВКМ_Сотрудник", Выборка.ВКМ_Специалист);
		
		МассивЭлементов = Новый Массив;
		Шрифт = Новый Шрифт(,,,Истина);
		
		МассивЭлементов.Добавить(Новый ФорматированнаяСтрока(Строка(Выборка.ВКМ_Клиент), Шрифт));
		МассивЭлементов.Добавить(Символы.ПС);
		МассивЭлементов.Добавить(Новый ФорматированнаяСтрока(Строка(Выборка.ВКМ_Договор), Шрифт));
		МассивЭлементов.Добавить(Символы.ПС);
		
		Если ЗначениеЗаполнено(Выборка.ВКМ_ОписаниеПроблемы) Тогда
			МассивЭлементов.Добавить(Строка("Описание проблемы: " + Выборка.ВКМ_ОписаниеПроблемы));
			МассивЭлементов.Добавить(Символы.ПС);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ВКМ_ОписаниеРабот) Тогда
			МассивЭлементов.Добавить(Строка( "Проделанные работы: " + Выборка.ВКМ_ОписаниеРабот));
			МассивЭлементов.Добавить(Символы.ПС);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ВКМ_ЧасыКОплатеКлиенту) Тогда
			МассивЭлементов.Добавить(Строка("Часы к оплате: " + Выборка.ВКМ_ЧасыКОплатеКлиенту));
			МассивЭлементов.Добавить(Символы.ПС);
		КонецЕсли;
		
		ЭлементПланировщика = Планировщик.Элементы.Добавить(НачалоПериода, КонецПериода);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеИзмерений);
		ЭлементПланировщика.Значение = Выборка.Ссылка;		
		ЭлементПланировщика.Текст = Новый ФорматированнаяСтрока(МассивЭлементов);
		
		Цвет = Выборка.ВКМ_Цвет.Получить();
		Если Цвет = Неопределено Тогда
			Цвет = WebЦвета.Древесный;			
		КонецЕсли;
		ЭлементПланировщика.ЦветФона = Цвет;
		 
	КонецЦикла;
	
КонецПроцедуры

 #КонецОбласти






